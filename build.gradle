plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.0'
    id 'maven-publish'
}

group = 'com.reevajs'
archivesBaseName = 'mfbt'
version = '1.0.0'
def RELEASE = true

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0'

    testImplementation 'org.junit.platform:junit-platform-runner:1.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.platform:junit-platform-suite-api:1.8.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5:1.6.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.1'
    testImplementation 'io.strikt:strikt-core:0.32.0'

}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
}

jar {
    from sourceSets.main.output
}

task jarWithSources(type: Jar) {
    classifier('sources')
    from sourceSets.main.allSource
}

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def publishingUsername = null
def publishingPassword = null

if (project.hasProperty("IS_CI")) {
    publishingUsername = System.getenv("REPOSILITE_USER")
    publishingPassword = System.getenv("REPOSILITE_PASSWORD")
} else if (project.hasProperty("reposilite_user")) {
    publishingUsername = project.property("reposilite_user")
    publishingPassword = project.property("reposilite_password")
}

if (publishingUsername != null && publishingPassword != null) {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name

                if (RELEASE) {
                    version = project.version
                } else {
                    version = getGitHash()
                }

                artifact jarWithSources

                from components.kotlin
            }
        }

        repositories {
            maven {
                name 'reposilite'
                url = 'https://repo.mattco.me/' + (RELEASE ? 'releases' : 'snapshots')

                credentials {
                    username = publishingUsername
                    password = publishingPassword
                }

                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}
